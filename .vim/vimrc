" Required Starting Settings ---------------------------------------------- {{{
set nocompatible              " be iMproved, required
filetype off                  " required
" }}}

" Plugin Management ------------------------------------------------------- {{{
"
" Set the runtime path to include Vundle and initialize
set rtp+=~/.vim/bundle/Vundle.vim
call vundle#begin()
" Alternatively, pass a path where Vundle should install plugins
"call vundle#begin('~/some/path/here')

" Let Vundle manage Vundle, required
Plugin 'VundleVim/Vundle.vim'

" NERDTree plugin
Plugin 'scrooloose/nerdTree'

" NERD Commenter
Plugin 'scrooloose/nerdcommenter'

" Solarized color scheme for Airline
Plugin 'vim-airline/vim-airline-themes'

" python-mode plugin
Plugin 'klen/python-mode'

" Airline (alternate vim-only version of now depricated vim-powerline)
Plugin 'vim-airline/vim-airline'

" Git plugin to allow branch info to show
Plugin 'tpope/vim-fugitive'

" Git plugin to indicate lines with changes
Plugin 'airblade/vim-gitgutter'

" vim-obsession to make Session.vim better
Plugin 'tpope/vim-obsession'

" vim-surround to work with HTML, XML, etc.
Plugin 'tpope/vim-surround'

" vim-unimpaired to quickly toggle and navigate
Plugin 'tpope/vim-unimpaired'

" syntastic linter
Plugin 'scrooloose/syntastic'

" CtrlP Fuzzy Finder
Plugin 'ctrlpvim/ctrlp.vim'

" ack.vim
" Note that this requires ack (`brew install ack`)
Plugin 'mileszs/ack.vim'

" phpcomplete for improved omni-completion in PHP. Use <C-x><C-o>
Plugin 'shawncplus/phpcomplete.vim'

" phpfolding for automatically creating folds in PHP code
Plugin 'rayburgemeestre/phpfolding.vim'

" php syntax highlighting
Plugin 'stanangeloff/php.vim'

" python syntax support
Plugin 'hdima/python-syntax'

" Vim Mustache
Plugin 'mustache/vim-mustache-handlebars'

" Twig.vim
Plugin 'nelsyeung/twig.vim'

" The following are examples of different formats supported.
" Keep Plugin commands between vundle#begin/end.
" plugin on GitHub repo
" Plugin 'tpope/vim-fugitive'
" " plugin from http://vim-scripts.org/vim/scripts.html
" Plugin 'L9'
" " Git plugin not hosted on GitHub
" Plugin 'git://git.wincent.com/command-t.git'
" " git repos on your local machine (i.e. when working on your own plugin)
" Plugin 'file:///home/gmarik/path/to/plugin'
" " The sparkup vim script is in a subdirectory of this repo called vim.
" " Pass the path to set the runtimepath properly.
" Plugin 'rstacruz/sparkup', {'rtp': 'vim/'}
" " Avoid a name conflict with L9
" Plugin 'user/L9', {'name': 'newL9'}
"
" All of your Plugins must be added before the following line
call vundle#end()            " required
" filetype plugin indent on    " required
" To ignore plugin indent changes, instead use:
filetype plugin on
"
" Brief help
" :PluginList       - lists configured plugins
" :PluginInstall    - installs plugins; append `!` to update or just
" :PluginUpdate
" :PluginSearch foo - searches for foo; append `!` to refresh local cache
" :PluginClean      - confirms removal of unused plugins; append `!` to
" auto-approve removal

" See :h vundle for more details or wiki for FAQ
" Put your non-Plugin stuff after this line
" }}}

" General Vim Settings ---------------------------------------------------- {{{
"
" set t_Co=256

" Allow vim's own fuzzy find to work
set path+=**

set background=dark

colorscheme solarized

" Allow background color changes to also work in tmux
if &term =~ '256color'
	set t_ut=
endif

" Enable syntax highlighting
syntax on

" Set python syntax highlighting to highest level
let python_highlight_all=1


" Centralize backups, swapfiles and undo history
set backupdir=~/.vim/backups
set directory=~/.vim/swaps
if exists("&undodir")
	set undodir=~/.vim/undo
endif

" Make default clipboard the OS X clipboard (and unnamedplus for Linux)
set clipboard=unnamed,unnamedplus

" Enable the mouse
set mouse=a
if has("mouse_sgr")
	" enable resizing splits with mouse in tmux
	set ttymouse=sgr
elseif &term =~ '^screen'
	" tmux knows the extended mouse mode
	set ttymouse=xterm2
endif

" Enable line numbers
set number

" Always show status line
set laststatus=2

" Disable error bells
set noerrorbells

" Show the cursor position
set ruler

" Highlight the cursor line by default
set cursorline

" Allow deletion of existing characters in insert mode
set backspace=2

" Show the filename in the window titlebar
set title

" Start scrolling three lines before the horizontal window border
set scrolloff=3

" Highlight matching pairs as you type: (), [], {}
set showmatch

" Search-as-you-type
set incsearch

" Case-insensitive searching
set ignorecase

" Case-sensitive if expression contains a capital letter
set smartcase

" Use highlighting for search matches (:nohlsearch to clear [or :noh])
set hlsearch

" Show tabs as '‚ñ∏' followed by spaces
" set listchars=tab:‚ñ∏\ ,eol:¬¨
set listchars=tab:‚ñ∏\ ,trail:-

" Set tabstop to 4 spaces
set tabstop=4

" Set tab width to 4 spaces
" set shiftwidth=4

" Expand tabs into spaces
" set expandtab

" Set minimum window width to 88 (a.k.a. 'wiw')
set winwidth=88

filetype plugin indent on

" Set the default actual tab key to 4 spaces
set softtabstop=4

" Start file with open folds
set nofoldenable

" Toggle folds open/closed with space bar (ignore errors)
nnoremap <silent> <Space> :silent! normal! zA<CR>

" Make omnicompletion better by selecting highlighted with Enter
set completeopt=longest,menuone
inoremap <expr> <CR> pumvisible() ? "\<C-y>" : "\<C-g>u\<CR>"

" Enable ability to load man pages in Vim (with ":Man")
runtime! ftplugin/man.vim
" }}}

" Gvim Settings ----------------------------------------------------------- {{{
"
" Prevent blinking cursor
set guicursor+=a:blinkon0

" Make cursor shape an underline
set guicursor+=a:hor10

" Hide scrollbars
set guioptions-=r
set guioptions-=L
" }}}

" File-Specific Settings -------------------------------------------------- {{{
"
" To force hard tabs, set noexpandtab and don't set softtabstop
"   tabstop and shiftwidth should be set to the same value
" To force spaces instead of tabs, set expandtab and softtabstop
"   shiftwidth and softtabstop should be set to the same value
augroup filetypeSpecific
	autocmd!
	autocmd FileType crontab    setlocal nobackup nowritebackup
	autocmd FileType help       setlocal nonumber
	autocmd FileType html       setlocal noexpandtab tabstop=2 shiftwidth=2 noeol binary
	autocmd FileType htmldjango setlocal noexpandtab           shiftwidth=2
	autocmd FileType javascript setlocal noexpandtab tabstop=4 shiftwidth=4 softtabstop=0
	autocmd FileType json       setlocal noexpandtab tabstop=2 shiftwidth=2
	autocmd FileType mustache   setlocal noendofline binary
	autocmd FileType php        setlocal noexpandtab tabstop=4 shiftwidth=4 foldmethod=manual
	autocmd FileType python     setlocal expandtab   tabstop=8 shiftwidth=4 softtabstop=4
	autocmd FileType vim        setlocal noexpandtab tabstop=4 shiftwidth=4 foldmethod=marker
augroup END
" }}}

" Key Mappings ------------------------------------------------------------ {{{
"
" Change <Leader> to ',' (comma)
let mapleader=","

" Change <LocalLeader> to '\' (backslash)
let maplocalleader="\\"

" Remap save
nnoremap <Leader>s :w<CR>

" Remap load
nnoremap <Leader>e :e!<CR>
nnoremap <Leader>E :tabdo windo e!<CR>

" Make 'Y' copy from current location to end of line
noremap Y y$

" Remap escape when in insert mode
inoremap jj <ESC>

" Toggle paste mode (retains incomming formatting)
noremap <silent> <F2> :set paste!<CR>

" Have z. perform like zz (because zz is redefined below)
nnoremap z. zz

" Make moving between windows easier
noremap <C-h> <C-w>h
noremap <C-j> <C-w>j
noremap <C-k> <C-w>k
noremap <C-l> <C-w>l

" Avoid accidentally triggering the help when reaching for Escape
inoremap <F1> <ESC>
nnoremap <F1> <ESC>
vnoremap <F1> <ESC>

" Make command mode go to beginning of line with Ctrl+a
cnoremap <C-a>  <Home>

" * will search word under cursor, but won't immediately jump to first match
nnoremap * :keepjumps normal! mi*`i<CR>

" Turn off highlighting
nnoremap <silent> <Leader>n :noh<CR>

" Search in PHP files
command! -nargs=+ Pgrep grep -rn --include={*.php,} <args>

" Search in all files
command! -nargs=+ Agrep grep -rn <args>

" Run ctags specifically for php
nnoremap <Leader>ctp :silent !ctags -R --fields=+laimS --languages=php .<CR>:redraw!<CR>

" Switch to previously active tab with <Leader>l
let g:lasttab = 1
nnoremap <Leader>l :exe "tabn ".g:lasttab<CR>
augroup tabLeave
	autocmd!
	autocmd TabLeave * let g:lasttab = tabpagenr()
augroup END

" Close a whole tab, no matter how many windows are in it
nnoremap <silent> <Leader>C :tabc<CR>

" Edit vimrc file
nnoremap <Leader>ve :vsplit $MYVIMRC<CR>

" Source vimrc file
nnoremap <silent> <Leader>vs :source $MYVIMRC<CR> :AirlineRefresh<CR>
" }}}

" Macros and Registers ---------------------------------------------------- {{{
"
" Change end-of-line digits (default is to add)
call setreg('d', 's/\d\+$/\=submatch(0)+')

" Align equal sign to value of b:i (e.g., `:let b:i=30`)
call setreg('a', '0f=100i :execute "normal! =b:i|"dw')

" Example of how to save a macro
" let @q = ''
" }}}

" User-Defined Functions -------------------------------------------------- {{{
"
" Indent first instance of '=' to specified column. Allows optional second
" parameter to specify a character other than '='.
"
" Example: `:call AJIndent(30, ':')` with cursor on the desired line
" Note: Also works on blocks of text in Visual mode
function! AJIndent(indent_column, ...)
	" If an alignment character was specified, use it, otherwise use '='
	" (Uses ternary if-then-else expression; see `:help E109` for details.)
	let s:pattern = (a:0 > 0) ? a:1 : '='
	if (len(a:align_char) > 1)
		echohl WarningMsg
		echom "Must specify single character on which to align"
		echohl None
	endif
	let s:line = getline(line('.'))
	let s:match_found = match(s:line, s:pattern)
	if s:match_found == -1
		return
	endif
	let s:match_locations = matchstrpos(s:line, '^.*' . s:pattern)
	if s:match_locations[2] > a:indent_column
		let s:between = matchstrpos(s:line, '\S\s*'. s:pattern)
		if s:between[1] > a:indent_column
			echohl WarningMsg
			echom 'Not enough whitespace before pattern occurs'
			echohl None
			return
		endif
	endif
	execute 'normal! 0f' . s:pattern .
			\ a:indent_column . 'i ' .
			\ a:indent_column . '|dw'
endfunction
" Map this function to <Leader>i
nnoremap <Leader>i :call AJIndent(
vnoremap <Leader>i :call AJIndent(

" Toggle background between solarized light and dark
function! BgToggle()
	if (&background == "light")
		let g:solarized_contrast="normal"
		set background=dark
	else
		let g:solarized_contrast="high"
		set background=light
	endif
	" Re-enable italics
	if &term =~ '-italic'
		hi Comment cterm=italic
		hi htmlArg cterm=italic
	endif
endfunction
" Map this function to <F9>
nnoremap <silent> <F9> :call BgToggle()<CR>


function! ReadingHeight()
	execute "normal! zz" . winheight(0)/4 . ""
endfunction
" Have zz put the screen up at a nice reading level
nnoremap zz :call ReadingHeight()<CR>
" }}}

" Load Local Settings ----------------------------------------------------- {{{
if filereadable(expand("~/.dotfiles/.vim/.vimrc_add_ons"))
	source ~/.dotfiles/.vim/.vimrc_add_ons
endif
" }}}

" Ack Settings ------------------------------------------------------------ {{{
"
" Also see The_Silver_Searcher settings
" Start an Ack search
nnoremap <Leader>a :Ack<Space>'

" Start an Ack search in a new tab
nnoremap <Leader>A :tabe<CR>:Ack<Space>'
" }}}

" Airline Settings -------------------------------------------------------- {{{
"
let g:airline_powerline_fonts = 1
let g:airline_theme = 'solarized'
let g:airline#extensions#whitespace#enabled = 0
let g:airline#extensions#whitespace#mixed_indent_algo = 2
let g:airline#extensions#whitespace#checks = [ 'indent', 'trailing', 'mixed-indent-file' ]

" to use fancy symbols for airline, uncomment the following lines and use a
" patched font (more info on the README.rst)
if !exists('g:airline_symbols')
	let g:airline_symbols = {}
endif
let g:airline_left_sep = '‚ÆÄ'
let g:airline_left_alt_sep = '‚ÆÅ'
let g:airline_right_sep = '‚ÆÇ'
let g:airline_right_alt_sep = '‚ÆÉ'
let g:airline_symbols.branch = '‚≠†'
let g:airline_symbols.readonly = '‚≠§'
let g:airline_symbols.linenr = '‚≠°'
let g:airline_symbols.maxlinenr = '‚ò∞'
" }}}

" CtrlP Settings ---------------------------------------------------------- {{{
"
" Also see The_Silver_Searcher settings
" Note: Refresh cache with <F5>
let g:ctrlp_by_filename = 1
let g:ctrlp_extensions = ['autoignore']
let g:ctrlp_map = ''
let g:ctrlp_match_window = 'bottom,order:btt,min:1,max:10,results:30'
" let g:ctrlp_open_new_file = 'h'
let g:ctrlp_regexp = 1
" let g:ctrlp_show_hidden = 1
nnoremap <silent> <Leader>b :CtrlPBuffer<CR>
nnoremap <silent> <Leader>t :CtrlP<CR>
nnoremap <silent> <Leader>T :CtrlPTag<CR>
" }}}

" Fugitive Settings ------------------------------------------------------- {{{
"
nnoremap <silent> <Leader>gb :Gblame<CR>
augroup fugitiveBuffers
	autocmd!
	autocmd BufReadPost fugitive://* set bufhidden=delete
augroup END
" }}}

" NERDCommenter Settings -------------------------------------------------- {{{
"
let g:NERDSpaceDelims=1
let g:NERDTrimTrailingWhitespace=1
" }}}

" NERDTree Settings ------------------------------------------------------- {{{
"
" Toggle NERDTree display
nnoremap <silent> <Leader>d :NERDTreeToggle<CR>
" Open NERDTree with the current file selected
nnoremap <silent> <Leader>f :NERDTreeFind<CR>

" Show hidden files by default
let NERDTreeShowHidden=1

" Don't show these file types
let g:NERDTreeIgnore = ['\.pyc$', '\.pyo$']

let g:NERDTreeWinSize = 40
let g:NERDTreeDirArrows = 1
let g:NERDTreeDirArrowExpandable = '‚ñ∏'
let g:NERDTreeDirArrowCollapsible = '‚ñæ'
" }}}

" phpfolding Settings ----------------------------------------------------- {{{
"
" Don't autofold
let g:DisableAutoPHPFolding = 1
" }}}

" PyMode Settings --------------------------------------------------------- {{{
"
" Ignore lines that are too long
let g:pymode_lint_ignore = 'E501'

" Set debug breakpoints
let g:pymode_breakpoint_bind = '<Leader>B'
" }}}

" Syntastic Settings ------------------------------------------------------ {{{
"
set statusline+=%#warningmsg#
set statusline+=%{SyntasticStatuslineFlag()}
set statusline+=%*

let g:syntastic_always_populate_loc_list = 1
let g:syntastic_auto_loc_list = 1
let g:syntastic_check_on_open = 1
let g:syntastic_check_on_wq = 0
let g:syntastic_mode_map = {
	\ 'mode': 'active',
	\ 'passive_filetypes': ['python', 'html']}
let g:syntastic_html_tidy_ignore_errors = [
	\  '<html> attribute "lang" lacks value',
	\  '<a> attribute "href" lacks value',
	\  'trimming empty <span>',
	\  'trimming empty <h1>',
	\  '<div> attribute "{{^" lacks value',
	\  '<div> attribute "}}" lacks value',
	\  '<div> attribute "{{/" lacks value',
	\ ]
" }}}

" The_Silver_Searcher Settings -------------------------------------------- {{{
"
" Use The Silver Searcher https://github.com/ggreer/the_silver_searcher
if executable('ag')
	" Use Ag over Grep
	set grepprg=ag\ --nogroup\ --nocolor

	" Use Ag over Ack
	let g:ackprg = 'ag --vimgrep'

	" Use ag in CtrlP for listing files. Lightning fast and respects
	" .gitignore
	let g:ctrlp_user_command = 'ag %s -l --nocolor -g ""'

	" ag is fast enough that CtrlP doesn't need to cache
	let g:ctrlp_use_caching = 0
endif
" }}}

" Vim Mustache Settings --------------------------------------------------- {{{
"
let g:mustache_abbreviations = 1
" }}}

" PHP Syntax Overrides ---------------------------------------------------- {{{
"
" This appears out of alphabetical order to ensure no interference
function! PhpSyntaxOverride()
  hi! def link phpDocTags  phpDefine
  hi! def link phpDocParam phpType
endfunction

augroup phpSyntaxOverride
  autocmd!
  autocmd FileType php call PhpSyntaxOverride()
augroup END
" }}}

" Ensure Italics ---------------------------------------------------------- {{{
" If our terminal supports italics, use them!
if &term =~ '-italic'
	hi Comment cterm=italic
	hi htmlArg cterm=italic
endif
" }}}
